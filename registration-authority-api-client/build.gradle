buildscript {
    ext {
        swaggerCoreVersion = '1.6.8'
        jacksonDatabindNullableVersion = '0.2.4'
        jsr305Version = '3.0.2'
    }
}

plugins {
    id 'org.springframework.boot' version '2.7.6' apply false
    id 'io.spring.dependency-management' version '1.1.0'
    id "org.openapi.generator" version "6.2.1"
    id "de.undercouch.download" version "5.3.0"
    id 'java-library'
}

dependencyManagement {
    imports {
        mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
    }
}

dependencies {
    api "com.google.code.findbugs:jsr305:$jsr305Version"
    api "io.swagger:swagger-annotations:$swaggerCoreVersion"
    api 'org.springframework:spring-web'
    api 'org.springframework:spring-context'
    api 'com.fasterxml.jackson.core:jackson-databind'
    api 'com.fasterxml.jackson.core:jackson-annotations'
    api 'com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider'
    api 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    compileOnly 'jakarta.annotation:jakarta.annotation-api'
    api "org.openapitools:jackson-databind-nullable:$jacksonDatabindNullableVersion"
}

task downloadFile(type: Download) {
    src "https://raw.githubusercontent.com/SwissSign-AG/RaApi/main/api.json"
    dest buildDir
    onlyIfModified true
    useETag true
}

openApiGenerate {
    inputSpec = "$buildDir/api.json"
    apiPackage = "ch.swisssign.ra.gen.client.api"
    invokerPackage = "ch.swisssign.ra.gen.client.invoker"
    modelPackage = "ch.swisssign.ra.gen.client.model"
    generateApiTests = false
    generateModelTests = false
    modelNameSuffix = "Dto"
    generatorName = "java"
    library = "resttemplate"
    configOptions = [
            dateLibrary: "java8"
    ]
    skipValidateSpec = false
    logToStderr = true
    generateAliasAsModel = false
    enablePostProcessFile = false
}

tasks.openApiGenerate.dependsOn tasks.downloadFile

compileJava.dependsOn tasks.openApiGenerate

sourceSets {
    main {
        java {
            srcDir new File(buildDir, "generate-resources/main/")
        }
    }
}
